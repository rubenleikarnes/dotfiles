#
# startup file read in interactive login shells
#
# The following code helps us by optimizing the existing framework.
# This includes zcompile, zcompdump, etc.
#

(
  local dir file
  setopt LOCAL_OPTIONS EXTENDED_GLOB
  autoload -U zrecompile

  # zcompile the completion cache; siginificant speedup
  zrecompile -pq ${ZDOTDIR:-${HOME}}/${zcompdump_file:-.zcompdump}

  # zcompile .zshrc
  zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshrc

  # zcompile enabled module autoloaded functions
  for dir in ${ZIM_HOME}/modules/${^zmodules}/functions(/FN); do
    zrecompile -pq ${dir}.zwc ${dir}/^([_.]*|prompt_*_setup|README*|*.zwc|*.zwc.old)(-.N)
  done

  # zcompile enabled module init scripts
  for file in ${ZIM_HOME}/modules/${^zmodules}/init.zsh(-.N); do
    zrecompile -pq ${file}
  done

  # zcompile all zplug plugins
  for file in /usr/local/opt/zplug/repos/*/*/*.zsh(-.N); do
    zrecompile -pq ${file}
  done
) &!

# Initialize Zim
source ${ZIM_HOME}/login_init.zsh -q &!
